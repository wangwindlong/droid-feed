apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

ext.app = [
        'version'            : '2.2.0',
        'version_code'       : 24,
        'compile_sdk_version': 29,
        'min_sdk_version'    : 21,
        'target_sdk_version' : 29,
        'build_tools_version': '29.0.3'
]

android {
    compileSdkVersion app.compile_sdk_version
    buildToolsVersion app.build_tools_version

    defaultConfig {
        applicationId "com.droidfeed"
        minSdkVersion project.app.min_sdk_version
        targetSdkVersion project.app.target_sdk_version
        versionName project.app.version
        versionCode project.app.version_code
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', "DROIDFEED_GITHUB_URL", "\"https://github.com/dgngulcan/droid-feed\""
        buildConfigField 'String', "DROIDFEED_PRIVACY_POLICY", "\"https://app.termly.io/document/privacy-policy/ab750f11-7f6d-499d-aafb-57697bd8aba0\""
        buildConfigField 'String', "DROIDFEED_PREVIOUS_ISSUES", "\"https://us12.campaign-archive.com/home/?u=f04785b3cff4f9a551bab8831&id=b45d2a62b0\""

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        ignoreWarnings true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.material_design
    implementation deps.androidx.core_ktx
    implementation deps.androidx.fragment
    implementation deps.androidx.browser
    implementation deps.androidx.lifecycle
    implementation deps.androidx.lifecycle_ktx
    implementation deps.androidx.viewmodel_ktx
    kapt deps.androidx.lifecycle_compiler

    //rx
//    implementation deps.rxjava
//    implementation deps.rxandroid
//    implementation deps.rxkotlin

    implementation deps.androidx.paging
    implementation deps.androidx.constraint_layout
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.dagger.core
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.processor
    implementation deps.glide.core
    kapt deps.glide.compiler
    implementation deps.retrofit.core
    implementation deps.retrofit.moshi_converter
    implementation deps.okhttp.core
    implementation deps.okhttp.logging_interceptor
    implementation deps.crashlytics
    implementation deps.lottie
    implementation deps.firebase.core
    implementation deps.firebase.config
    implementation deps.firebase.firestore
    implementation deps.jsoup
    implementation deps.gson // quick fix for version conflict between room and firestore
    implementation deps.timber

    testImplementation deps.mockk
    testImplementation deps.junit
    testImplementation deps.kotlin.coroutines_test
    testImplementation deps.testx.core
    testImplementation deps.arch_testing
    androidTestImplementation deps.arch_testing
    androidTestImplementation deps.testx.core
    androidTestImplementation deps.testx.runner
    androidTestImplementation deps.testx.espresso
    androidTestImplementation deps.testx.rules
    androidTestImplementation deps.testx.monitor
    androidTestImplementation deps.testx.ext
    androidTestImplementation deps.room.testing
    detektPlugins deps.detekt_formatting
}

apply plugin: 'com.google.gms.google-services'
